apply plugin: 'com.android.application'
apply plugin: 'maven'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 27

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {

        //read version file
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        //read VERSION_CODE param from version.properties file
        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.okan.headlessgateway"
            versionCode code
            versionName "0.9." + (versionProps['VERSION_CODE'].toInteger() - 8)
            minSdkVersion 16
            targetSdkVersion 27
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    signingConfigs {
        //APK sign parameters
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
        debug {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                    //rename new APK with version params
                variant -> renameAPK(variant, defaultConfig, 'R')
            }
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:support-annotations:28.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.code.gson:gson:2.6.2'
    implementation 'com.ardic.android:IoTIgnite:0.8.2'
    implementation 'com.ardic.android:utilitylib:0.1'
    implementation 'com.ardic.android:connectivitylib:0.2'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'org.jmdns:jmdns:3.5.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'org.mockito:mockito-android:2.7.22'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.afollestad.material-dialogs:core:0.9.1.0'
    implementation 'com.ardic.android.iot:HwNodeAppTemplates:1.0'

    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://repo.maven.apache.org/maven2"
    }
    maven {
        url "https://repo.iot-ignite.com/content/repositories/releases"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = 'com.ardic.android.iot'
            pom.artifactId = 'HwNodeAppTemplates'
            pom.version = android.defaultConfig.versionName
            def homePath = System.properties['user.home']
            repository(url: "file://$homePath/.m2/repository")
        }
    }
}

def renameAPK(variant, defaultConfig, buildType) {
    variant.outputs.all { output ->
        def formattedDate = new Date().format('yyyyMMdd')
        def fileName = applicationName + "_v" + defaultConfig.versionName + "-" + formattedDate + "-" + buildType + ".apk"
        outputFileName = fileName
    }
}